import '../entities.puck' as {File, Type}
import 'span.puck' as {Position, Span, ToSpan}
import 'token.puck' as {SimpleToken}

export type Module {
  fileName: String
  path: String
  exports: ObjectMap<ExportDirective>
  statements: List<TopLevelStatement>
  file: File
}

export enum TopLevelStatement {
  ExportDirective(ExportDirective)
  ImportDirective(ImportDirective)

  EnumDeclaration(EnumDeclaration)
  ImplDeclaration(ImplDeclaration)
  ImplShorthandDeclaration(ImplShorthandDeclaration)
  TraitDeclaration(TraitDeclaration)
  TypeDeclaration(TypeDeclaration)

  BlockLevelStatement(BlockLevelStatement)
}

export enum BlockLevelStatement {
  Block(Block)
  BreakStatement(BreakStatement)
  ReturnStatement(ReturnStatement)
  WhileLoop(WhileLoop)

  Expression(Expression)
}

export enum Expression {
  ThrowStatement({expression: Expression})
  Comment(Comment)
  Identifier(Identifier)

  FunctionDeclaration(FunctionDeclaration)
  VariableDeclaration(VariableDeclaration)

  AssignmentExpression(AssignmentExpression)
  BinaryExpression(BinaryExpression)
  CallExpression(CallExpression)
  IfExpression(IfExpression)
  IfLetExpression(IfLetExpression)
  MatchExpression(MatchExpression)
  TypePathExpression(TypePathExpression)
  UnaryExpression(UnaryExpression)

  IndexAccess(IndexAccess)
  MemberAccess(MemberAccess)
  UnknownAccess(UnknownAccess)
  UnknownIndexAccess(UnknownIndexAccess)

  BooleanLiteral(BooleanLiteral)
  ListLiteral(ListLiteral)
  NumberLiteral(NumberLiteral)
  RecordLiteral(RecordLiteral)
  StringLiteral(StringLiteral)
  TupleLiteral(TupleLiteral)
}

export enum SimpleLiteral {
  BooleanLiteral(BooleanLiteral)
  NumberLiteral(NumberLiteral)
  StringLiteral(SimpleStringLiteral)
}

impl TopLevelStatement {
  fn getType(self) -> Type {
    match self {
      TopLevelStatement::ExportDirective(e) => throw 'type on export'
      TopLevelStatement::ImportDirective(e) => throw 'type on import'

      TopLevelStatement::EnumDeclaration(e) => e->type_.transmute()
      TopLevelStatement::ImplDeclaration(e) => e.type_->type_.transmute()
      TopLevelStatement::ImplShorthandDeclaration(e) => e.type_->type_.transmute()
      TopLevelStatement::TraitDeclaration(e) => e->type_.transmute()
      TopLevelStatement::TypeDeclaration(e) => e.type_

      TopLevelStatement::BlockLevelStatement(e) => e.getType()
    }
  }
}

impl ToSpan for TopLevelStatement {
  fn start(self) -> Position {
    match self {
      TopLevelStatement::ExportDirective(e) => e.start()
      TopLevelStatement::ImportDirective(e) => e.start()

      TopLevelStatement::EnumDeclaration(e) => e.start()
      TopLevelStatement::ImplDeclaration(e) => e.start()
      TopLevelStatement::ImplShorthandDeclaration(e) => e.start()
      TopLevelStatement::TraitDeclaration(e) => e.start()
      TopLevelStatement::TypeDeclaration(e) => e.start()

      TopLevelStatement::BlockLevelStatement(e) => e.start()
    }
  }
  fn end(self) -> Position {
    match self {
      TopLevelStatement::ExportDirective(e) => e.end()
      TopLevelStatement::ImportDirective(e) => e.end()

      TopLevelStatement::EnumDeclaration(e) => e.end()
      TopLevelStatement::ImplDeclaration(e) => e.end()
      TopLevelStatement::ImplShorthandDeclaration(e) => e.end()
      TopLevelStatement::TraitDeclaration(e) => e.end()
      TopLevelStatement::TypeDeclaration(e) => e.end()

      TopLevelStatement::BlockLevelStatement(e) => e.end()
    }
  }
}

impl BlockLevelStatement {
  fn getType(self) -> Type {
    match self {
      BlockLevelStatement::Block(e) => e.type_
      BlockLevelStatement::BreakStatement(e) => e->type_.transmute()
      BlockLevelStatement::ReturnStatement(e) => e->type_.transmute()
      BlockLevelStatement::WhileLoop(e) => e->type_.transmute()

      BlockLevelStatement::Expression(e) =>  e.getType()
    }
  }
}

impl ToSpan for BlockLevelStatement {
  fn start(self) -> Position {
    match self {
      BlockLevelStatement::Block(e) => e.start()
      BlockLevelStatement::BreakStatement(e) => e.start()
      BlockLevelStatement::ReturnStatement(e) => e.start()
      BlockLevelStatement::WhileLoop(e) => e.start()
      BlockLevelStatement::Expression(e) =>  e.start()
    }
  }
  fn end(self) -> Position {
    match self {
      BlockLevelStatement::Block(e) => e.end()
      BlockLevelStatement::BreakStatement(e) => e.end()
      BlockLevelStatement::ReturnStatement(e) => e.end()
      BlockLevelStatement::WhileLoop(e) => e.end()
      BlockLevelStatement::Expression(e) =>  e.end()
    }
  }
}

impl Expression {
  fn getType(self) -> Type {
    match self {
      Expression::ThrowStatement(e) => e->type_.transmute()
      Expression::Comment(_) => panic('getType on comment')

      Expression::Identifier(e) => e->type_.transmute()
      Expression::FunctionDeclaration(e) => e.type_
      Expression::VariableDeclaration(e) => e->type_.transmute()

      Expression::AssignmentExpression(e) => e->type_.transmute()
      Expression::BinaryExpression(e) => e->type_.transmute()
      Expression::CallExpression(e) => e->type_.transmute()
      Expression::IfExpression(e) => e->type_.transmute()
      Expression::IfLetExpression(e) => e->type_.transmute()
      Expression::MatchExpression(e) => e->type_.transmute()
      Expression::TypePathExpression(e) => e->type_.transmute()
      Expression::UnaryExpression(e) => e->type_.transmute()

      Expression::IndexAccess(e) => e->type_.transmute()
      Expression::MemberAccess(e) => e->type_.transmute()
      Expression::UnknownAccess(e) => e->type_.transmute()
      Expression::UnknownIndexAccess(e) => e->type_.transmute()

      Expression::BooleanLiteral(e) => e->type_.transmute()
      Expression::ListLiteral(e) => e->type_.transmute()
      Expression::NumberLiteral(e) => e->type_.transmute()
      Expression::RecordLiteral(e) => e->type_.transmute()
      Expression::StringLiteral(e) => e->type_.transmute()
      Expression::TupleLiteral(e) => e->type_.transmute()
    }
  }
}

impl ToSpan for Expression {
  fn start(self) -> Position {
    match self {
      Expression::ThrowStatement(e) => e.expression.start()
      Expression::Comment(_) => throw 'No span for Comment'

      Expression::Identifier(e) => e.start()
      Expression::FunctionDeclaration(e) => e.start()
      Expression::VariableDeclaration(e) => e.start()

      Expression::AssignmentExpression(e) => e.start()
      Expression::BinaryExpression(e) => e.start()
      Expression::CallExpression(e) => e.start()
      Expression::IfExpression(e) => e.start()
      Expression::IfLetExpression(e) => e.start()
      Expression::MatchExpression(e) => e.start()
      Expression::TypePathExpression(e) => e.start()
      Expression::UnaryExpression(e) => e.start()

      Expression::IndexAccess(e) => e.start()
      Expression::MemberAccess(e) => e.start()
      Expression::UnknownAccess(e) => e.start()
      Expression::UnknownIndexAccess(e) => e.start()

      Expression::BooleanLiteral(e) => e.start()
      Expression::ListLiteral(e) => e.start()
      Expression::NumberLiteral(e) => e.span.start
      Expression::RecordLiteral(e) => e.start()
      Expression::StringLiteral(e) => e.start()
      Expression::TupleLiteral(e) => e.start()
    }
  }
  fn end(self) -> Position {
    match self {
      Expression::ThrowStatement(e) => e.expression.end()
      Expression::Comment(_) => throw 'No span for Comment'

      Expression::Identifier(e) => e.end()
      Expression::FunctionDeclaration(e) => e.end()
      Expression::VariableDeclaration(e) => e.end()

      Expression::AssignmentExpression(e) => e.end()
      Expression::BinaryExpression(e) => e.end()
      Expression::CallExpression(e) => e.end()
      Expression::IfExpression(e) => e.end()
      Expression::IfLetExpression(e) => e.end()
      Expression::MatchExpression(e) => e.end()
      Expression::TypePathExpression(e) => e.end()
      Expression::UnaryExpression(e) => e.end()

      Expression::IndexAccess(e) => e.end()
      Expression::MemberAccess(e) => e.end()
      Expression::UnknownAccess(e) => e.end()
      Expression::UnknownIndexAccess(e) => e.end()

      Expression::BooleanLiteral(e) => e.end()
      Expression::ListLiteral(e) => e.end()
      Expression::NumberLiteral(e) => e.span.end
      Expression::RecordLiteral(e) => e.end()
      Expression::StringLiteral(e) => e.end()
      Expression::TupleLiteral(e) => e.end()
    }
  }
}

export type EnumDeclaration {
  attributes: List<Attribute>
  keyword: SimpleToken
  name: Identifier
  typeParameters: List<TypeParameter>
  openBrace: SimpleToken
  members: List<EnumMember>
  closeBrace: SimpleToken
}

impl ToSpan for EnumDeclaration {
  fn span(self) -> Span {{
    start: self.keyword.span.start
    end: self.closeBrace.span.end
  }}
}

export type EnumMember {
  name: Identifier
  bound: Option<TypeBound>
}

impl ToSpan for EnumMember {
  fn span(self) -> Span {{
    start: self.name.span.start
    end: self.bound.mapOr(self.name.span, |b| b.span()).end
  }}
}

export type ImplDeclaration {
  implKeyword: SimpleToken
  typeParameters: List<TypeParameter>

  trait_: NamedTypeBound
  forKeyword: SimpleToken
  type_: NamedTypeBound

  openBrace: SimpleToken
  members: List<FunctionDeclaration>
  closeBrace: SimpleToken
}

impl ToSpan for ImplDeclaration {
  fn span(self) -> Span {{
    start: self.implKeyword.span.start
    end: self.closeBrace.span.end
  }}
}

export type ImplShorthandDeclaration {
  implKeyword: SimpleToken
  typeParameters: List<TypeParameter>

  type_: NamedTypeBound

  openBrace: SimpleToken
  members: List<FunctionDeclaration>
  closeBrace: SimpleToken
}

impl ToSpan for ImplShorthandDeclaration {
  fn span(self) -> Span {{
    start: self.implKeyword.span.start
    end: self.closeBrace.span.end
  }}
}

export type TraitDeclaration {
  attributes: List<Attribute>
  keyword: SimpleToken
  name: Identifier
  typeParameters: List<TypeParameter>
  openBrace: SimpleToken
  members: List<FunctionDeclaration>
  closeBrace: SimpleToken
}

impl TraitDeclaration {
  fn getType(self) -> Type {self->type_.transmute()}
}

impl ToSpan for TraitDeclaration {
  fn span(self) -> Span {{
    start: self.keyword.span.start
    end: self.closeBrace.span.end
  }}
}

export type TypeDeclaration {
  attributes: List<Attribute>
  keyword: SimpleToken
  name: Identifier
  typeParameters: List<TypeParameter>
  bound: Option<TypeBound>

  type_: Type
}

impl ToSpan for TypeDeclaration {
  fn span(self) -> Span {{
    start: self.keyword.span.start
    end: self.bound.unwrap().end()
  }}
}

export type ExportDirective {
  keyword: SimpleToken
  statement: ExportedStatement
  identifier: Identifier
}

impl ExportDirective {
  fn getType(self) -> Type {
    match self.statement {
      ExportedStatement::EnumDeclaration(d) => d->type_.transmute()
      ExportedStatement::TraitDeclaration(d) => d->type_.transmute()
      ExportedStatement::TypeDeclaration(d) => d.type_
      ExportedStatement::FunctionDeclaration(d) => d.type_
      ExportedStatement::VariableDeclaration(d) => d->type_.transmute()
    }
  }
}

impl ToSpan for ExportDirective {
  fn span(self) -> Span {{
    start: self.keyword.span.start
    end: self.statement.end()
  }}
}

export enum ExportedStatement {
  EnumDeclaration(EnumDeclaration)
  TraitDeclaration(TraitDeclaration)
  TypeDeclaration(TypeDeclaration)
  FunctionDeclaration(FunctionDeclaration)
  VariableDeclaration(VariableDeclaration)
}

impl ToSpan for ExportedStatement {
  fn span(self) -> Span {
    match self {
      ExportedStatement::EnumDeclaration(d) => d.span()
      ExportedStatement::TraitDeclaration(d) => d.span()
      ExportedStatement::TypeDeclaration(d) => d.span()
      ExportedStatement::FunctionDeclaration(d) => d.span()
      ExportedStatement::VariableDeclaration(d) => d.span()
    }
  }
}

export type ImportDirective {
  importKeyword: SimpleToken
  locator: StringLiteral
  domain: Option<String>
  path: String
  asKeyword: SimpleToken
  specifier: ImportSpecifier
  module: Option<Module>
}

impl ToSpan for ImportDirective {
  fn span(self) -> Span {{
    start: self.importKeyword.span.start
    end: self.specifier.end()
  }}
}

export enum ImportSpecifier {
  Asterisk(SimpleToken)
  Identifier(Identifier)
  ObjectDestructure(ObjectDestructure)
}

impl ToSpan for ImportSpecifier {
  fn span(self) -> Span {
    match self {
      ImportSpecifier::Asterisk(token) => token.span
      ImportSpecifier::Identifier(identifier) => identifier.span
      ImportSpecifier::ObjectDestructure(objectDestructure) => objectDestructure.span()
    }
  }
}

export type ObjectDestructure {
  openBrace: SimpleToken
  members: List<ObjectDestructureMember>
  closeBrace: SimpleToken
}

impl ToSpan for ObjectDestructure {
  fn span(self) -> Span {{
    start: self.openBrace.span.start
    end: self.closeBrace.span.end
  }}
}

export type ObjectDestructureMember {
  property: Identifier
  local: Identifier
  file: Option<File>
}

impl ToSpan for ObjectDestructureMember {
  fn span(self) -> Span {{
    start: self.property.span.start
    end: self.local.span.end
  }}
}

export type Block {
  openBrace: Option<SimpleToken>
  statements: List<BlockLevelStatement>
  closeBrace: Option<SimpleToken>
  type_: Type
}

impl ToSpan for Block {
  fn start(self) -> Position {
    if let Some(openBrace) = self.openBrace
      then openBrace.span.start
      else self.statements.first().unwrap().start()
  }
  fn end(self) -> Position {
    if let Some(closeBrace) = self.closeBrace
      then closeBrace.span.end
      else self.statements.last().unwrap().end()
  }
}

export type BreakStatement {
  keyword: SimpleToken
}

impl ToSpan for BreakStatement {
  fn span(self) -> Span {
    self.keyword.span
  }
}

export type ReturnStatement {
  keyword: SimpleToken
  expression: Expression
}

impl ToSpan for ReturnStatement {
  fn span(self) -> Span {{
    start: self.keyword.span.start
    end: self.expression.end()
  }}
}

export type WhileLoop {
  condition: Expression
  body: Block
}

impl ToSpan for WhileLoop {
  fn span(self) -> Span {{
    start: self.condition.start()
    end: self.body.end()
  }}
}

export type Comment {
  text: String
  span: Span
}

export type Attribute {
  hash: SimpleToken
  openBracket: SimpleToken
  name: Identifier
  data: AttributeData
  closeBracket: SimpleToken
}

impl ToSpan for Attribute {
  fn span(self) -> Span {{
    start: self.hash.span.start
    end: self.closeBracket.span.end
  }}
}

export enum AttributeData {
  None
  Value(SimpleLiteral)
  Arguments(List<AttributeArgument>)
}

export type AttributeArgument {
  name: Identifier
  value: Option<SimpleLiteral>
}

export type Identifier {
  name: String
  span: Span
}

impl ToSpan for Identifier {
  fn span(self) -> Span {self.span}
}

export type FunctionDeclaration {
  name: Option<Identifier>
  typeParameters: List<TypeParameter>
  openParenOrBar: SimpleToken
  parameterList: List<VariableDeclaration>
  closeParenOrBar: SimpleToken
  returnType: Option<TypeBound>
  body: Option<Block>

  type_: Type
}

impl ToSpan for FunctionDeclaration {
  fn start(self) -> Position {
    self.name.mapOrElse(
      || {
        self.typeParameters.first().mapOrElse(
          || self.openParenOrBar.span.start
          |p| p.start()
        )
      }
      |i| i.span.start
    )
  }
  fn end(self) -> Position {
    self.body.mapOrElse(|| self.returnType.unwrap().end(), |b| b.end())
  }
}

export type VariableDeclaration {
  pattern: Pattern
  typeBound: Option<TypeBound>
  initializer: Option<Expression>
}

impl ToSpan for VariableDeclaration {
  fn start(self) -> Position {
    self.pattern.start()
  }
  fn end(self) -> Position {
    self.initializer.mapOrElse(|| self.typeBound.mapOrElse(|| self.pattern.end(), |t| t.end()), |i| i.end())
  }
}

export type AssignmentExpression {
  lhs: Expression
  token: SimpleToken
  rhs: Expression
}

impl ToSpan for AssignmentExpression {
  fn start(self) -> Position {
    self.lhs.start()
  }
  fn end(self) -> Position {
    self.rhs.end()
  }
}

export type BinaryExpression {
  lhs: Expression
  operator: SimpleToken
  rhs: Expression
}

impl ToSpan for BinaryExpression {
  fn start(self) -> Position {
    self.lhs.start()
  }
  fn end(self) -> Position {
    self.rhs.end()
  }
}

export type CallExpression {
  func: Expression
  typeArguments: Option<TypeArguments>
  openParen: SimpleToken
  argumentList: List<Expression>
  closeParen: SimpleToken
}

impl ToSpan for CallExpression {
  fn start(self) -> Position {
    self.func.start()
  }
  fn end(self) -> Position {
    self.closeParen.span.end
  }
}

export type TypeArguments {
  openBracket: SimpleToken
  typeArguments: List<TypeBound>
  closeBracket: SimpleToken
}

impl ToSpan for TypeArguments {
  fn start(self) -> Position {self.openBracket.span.start}
  fn end(self) -> Position {self.closeBracket.span.end}
}

export type IfExpression {
  ifKeyword: SimpleToken
  condition: Expression
  then_: Block
  else_: Option<Block>
}

impl ToSpan for IfExpression {
  fn start(self) -> Position {
    self.ifKeyword.span.start
  }
  fn end(self) -> Position {
    self.else_.unwrapOr(self.then_).end()
  }
}

export type IfLetExpression {
  ifKeyword: SimpleToken
  letKeyword: SimpleToken
  pattern: Pattern
  expression: Expression
  then_: Block
  else_: Option<Block>
}

impl ToSpan for IfLetExpression {
  fn start(self) -> Position {
    self.ifKeyword.span.start
  }
  fn end(self) -> Position {
    self.else_.unwrapOr(self.then_).end()
  }
}

export type MatchExpression {
  matchKeyword: SimpleToken
  expression: Expression
  openBrace: SimpleToken
  patterns: List<MatchArm>
  closeBrace: SimpleToken
}

impl ToSpan for MatchExpression {
  fn start(self) -> Position {
    self.matchKeyword.span.start
  }
  fn end(self) -> Position {
    self.closeBrace.span.end
  }
}

export type MatchArm {
  pattern: Pattern
  arrow: SimpleToken
  block: Block
}

impl ToSpan for MatchArm {
  fn start(self) -> Position {
    self.pattern.start()
  }
  fn end(self) -> Position {
    self.block.end()
  }
}

export type TypePathExpression {
  typePath: TypePath
}

impl ToSpan for TypePathExpression {
  fn span(self) -> Span {{
    start: self.typePath.start()
    end: self.typePath.end()
  }}
}

export enum TypePath {
  Object(Identifier, TypePath)
  Member(Identifier)
}

impl ToSpan for TypePath {
  fn span(self) -> Span {
    match self {
      TypePath::Object(identifier, typePath) => {{
        start: identifier.span.start
        end: typePath.end()
      }}
      TypePath::Member(identifier) => identifier.span
    }
  }
}

export type UnaryExpression {
  operator: SimpleToken
  rhs: Expression
}

impl ToSpan for UnaryExpression {
  fn span(self) -> Span {{
    start: self.operator.span.start
    end: self.rhs.end()
  }}
}

export type IndexAccess {
  object: Expression
  openBracket: SimpleToken
  index: Expression
  closeBracket: SimpleToken
}

impl ToSpan for IndexAccess {
  fn span(self) -> Span {{
    start: self.object.start()
    end: self.closeBracket.span.end
  }}
}

export type MemberAccess {
  object: Expression
  member: Identifier
}

impl ToSpan for MemberAccess {
  fn start(self) -> Position {
    self.object.start()
  }
  fn end(self) -> Position {
    self.member.span.end
  }
}

export type UnknownAccess {
  object: Expression
  member: Identifier
}

impl ToSpan for UnknownAccess {
  fn start(self) -> Position {
    self.object.start()
  }
  fn end(self) -> Position {
    self.member.span.end
  }
}

export type UnknownIndexAccess {
  object: Expression
  openBracket: SimpleToken
  index: Expression
  closeBracket: SimpleToken
}

impl ToSpan for UnknownIndexAccess {
  fn span(self) -> Span {{
    start: self.object.start()
    end: self.closeBracket.span.end
  }}
}

export type BooleanLiteral {
  keyword: SimpleToken
  value: Bool
}

impl ToSpan for BooleanLiteral {
  fn span(self) -> Span {self.keyword.span}
}

export type ListLiteral {
  openBracket: SimpleToken
  members: List<Expression>
  closeBracket: SimpleToken
}

impl ToSpan for ListLiteral {
  fn span(self) -> Span {{
    start: self.openBracket.span.start
    end: self.closeBracket.span.end
  }}
}

export type NumberLiteral {
  value: Num
  span: Span
}

impl ToSpan for NumberLiteral {
  fn span(self) -> Span {self.span}
}

export type RecordLiteral {
  openBrace: SimpleToken
  members: List<RecordLiteralMember>
  closeBrace: SimpleToken
}

impl ToSpan for RecordLiteral {
  fn span(self) -> Span {{
    start: self.openBrace.span.start
    end: self.closeBrace.span.end
  }}
}

export type RecordLiteralMember {
  name: Identifier
  value: Expression
}

impl ToSpan for RecordLiteralMember {
  fn start(self) -> Position {
    self.name.span.start
  }
  fn end(self) -> Position {
    self.value.end()
  }
}

export type StringLiteral {
  parts: List<StringLiteralPart>
}

impl ToSpan for StringLiteral {
  fn start(self) -> Position {
    self.parts.first().unwrap().start()
  }
  fn end(self) -> Position {
    self.parts.last().unwrap().end()
  }
}

export type SimpleStringLiteral {
  value: String
  span: Span
}

export enum StringLiteralPart {
  Literal(SimpleStringLiteral)
  Identifier(Identifier)
}

impl ToSpan for StringLiteralPart {
  fn span(self) -> Span {
    match self {
      StringLiteralPart::Literal({span}) => span
      StringLiteralPart::Identifier({span}) => span
    }
  }
}

export type TupleLiteral {
  openParen: SimpleToken
  expressions: List<Expression>
  closeParen: SimpleToken
}

impl ToSpan for TupleLiteral {
  fn span(self) -> Span {{
    start: self.openParen.span.start
    end: self.closeParen.span.end
  }}
}

export enum Pattern {
  CatchAll(SimpleToken)
  Identifier {identifier: Identifier, mutable: Bool}
  // Boolen(BooleanLiteral)
  // Number(NumberLiteral)
  // String(SimpleStringLiteral)
  Record(RecordPattern)
  Tuple(TuplePattern)
  RecordType(TypePath, RecordPattern)
  TupleType(TypePath, TuplePattern)
  UnitType(TypePath)
}

impl Pattern {
  fn displayName(self) -> String {
    match self {
      Pattern::CatchAll(_) => '_'
      Pattern::Identifier {identifier} => identifier.name
      Pattern::Record(recordPattern) => recordPattern.displayName()
      Pattern::Tuple(tuplePattern) => tuplePattern.displayName()
      Pattern::RecordType(_, recordPattern) => recordPattern.displayName()
      Pattern::TupleType(_, tuplePattern) => tuplePattern.displayName()
      Pattern::UnitType(_) => ''
    }
  }
}

impl ToSpan for Pattern {
  fn span(self) -> Span {
    match self {
      Pattern::CatchAll(token) => token.span
      Pattern::Identifier {identifier} => identifier.span
      Pattern::Record(recordPattern) => {{
        start: recordPattern.openBrace.span.start
        end: recordPattern.closeBrace.span.end
      }}
      Pattern::Tuple(tuplePattern) => {{
        start: tuplePattern.openParen.span.start
        end: tuplePattern.closeParen.span.end
      }}
      Pattern::RecordType(typePath, recordPattern) => {{
        start: typePath.start()
        end: recordPattern.closeBrace.span.end
      }}
      Pattern::TupleType(typePath, tuplePattern) => {{
        start: typePath.start()
        end: tuplePattern.closeParen.span.end
      }}
      Pattern::UnitType(typePath) => typePath.span()
    }
  }
}

export type RecordPattern {
  openBrace: SimpleToken
  properties: List<RecordPatternMember>
  closeBrace: SimpleToken
}

impl RecordPattern {
  fn displayName(self) -> String {
    '{' +
      self.properties.map(|p| {
        let shorthand =
          if let Pattern::Identifier {identifier: {name}} = p.pattern
            then name == p.property.name
            else false

        if shorthand
          then p.property.name
          else p.property.name + ': ' + p.pattern.displayName()
      })->join(', ') +
    '}'
  }
}

impl ToSpan for RecordPattern {
  fn start(self) -> Position {
    self.openBrace.span.start
  }
  fn end(self) -> Position {
    self.closeBrace.span.end
  }
}

export type RecordPatternMember {
  property: Identifier
  pattern: Pattern
}

impl ToSpan for RecordPatternMember {
  fn start(self) -> Position {
    self.property.span.start
  }
  fn end(self) -> Position {
    self.pattern.end()
  }
}

export type TuplePattern {
  openParen: SimpleToken
  properties: List<Pattern>
  closeParen: SimpleToken
}

impl TuplePattern {
  fn displayName(self) -> String {
    '(' + self.properties.map(|p| p.displayName())->join(', ') + ')'
  }
}

impl ToSpan for TuplePattern {
  fn start(self) -> Position {
    self.openParen.span.start
  }
  fn end(self) -> Position {
    self.closeParen.span.end
  }
}

export enum TypeBound {
  FunctionTypeBound(FunctionTypeBound)
  NamedTypeBound(NamedTypeBound)
  RecordTypeBound(RecordTypeBound)
  TupleTypeBound(TupleTypeBound)
}

impl TypeBound {
  fn getType(self) -> Type {
    match self {
      TypeBound::FunctionTypeBound(t) => t->type_.transmute()
      TypeBound::NamedTypeBound(t) => t->type_.transmute()
      TypeBound::RecordTypeBound(t) => t->type_.transmute()
      TypeBound::TupleTypeBound(t) => t->type_.transmute()
    }
  }

  fn getRecordTypeBound(self) -> RecordTypeBound {
    match self {
      TypeBound::RecordTypeBound(record) => record
      _ => throw 'TypeBound is not a RecordTypeBound'
    }
  }

  fn getTupleTypeBound(self) -> TupleTypeBound {
    match self {
      TypeBound::TupleTypeBound(tuple) => tuple
      _ => throw 'TypeBound is not a TupleTypeBound'
    }
  }
}

impl ToSpan for TypeBound {
  fn span(self) -> Span {
    match self {
      TypeBound::FunctionTypeBound(t) => t.span()
      TypeBound::NamedTypeBound(t) => t.span()
      TypeBound::RecordTypeBound(t) => t.span()
      TypeBound::TupleTypeBound(t) => t.span()
    }
  }
}

export type FunctionTypeBound {
  typeParameters: List<TypeParameter>
  parameters: TupleTypeBound
  returnType: TypeBound
}

impl ToSpan for FunctionTypeBound {
  fn span(self) -> Span {{
    start: self.typeParameters.first().mapOrElse(|| self.parameters.span(), |p| p.span()).start
    end: self.returnType.end()
  }}
}

export type NamedTypeBound {
  path: TypePath
  typeParameters: List<TypeBound>
}

impl NamedTypeBound {
  fn getType(self) -> Type {
    self->type_.transmute()
  }
}

impl ToSpan for NamedTypeBound {
  fn span(self) -> Span {{
    start: self.path.start()
    end: self.typeParameters.last().mapOr(self.path.span(), |p| p.span()).end
  }}
}

export type RecordTypeBound {
  openBrace: SimpleToken
  properties: List<RecordTypeBoundMember>
  closeBrace: SimpleToken
}

impl ToSpan for RecordTypeBound {
  fn span(self) -> Span {{
    start: self.openBrace.span.start
    end: self.closeBrace.span.end
  }}
}

export type RecordTypeBoundMember {
  name: Identifier
  typeBound: TypeBound
}

impl ToSpan for RecordTypeBoundMember {
  fn start(self) -> Position {
    self.name.span.start
  }

  fn end(self) -> Position {
    self.typeBound.end()
  }
}

export type TupleTypeBound {
  openParen: SimpleToken
  properties: List<TypeBound>
  closeParen: SimpleToken
}

impl ToSpan for TupleTypeBound {
  fn start(self) -> Position {
    self.openParen.span.start
  }

  fn end(self) -> Position {
    self.closeParen.span.end
  }
}

export type TypeParameter {
  name: Identifier
  defaultValue: Option<TypeBound>
}

impl ToSpan for TypeParameter {
  fn span(self) -> Span {{
    start: self.name.span.start
    end: self.name.span.end
  }}
}
