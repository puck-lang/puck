import 'puck:js' as {Object, RegExp, asResult, require}
import 'node:fs' as {statSync}
import 'node:path' as path
import '../ast/ast.puck' as {
  Module
  ImportDirective
}
import '../ast/visit.puck' as visit
import '../entities.puck' as {UnparsedFile}
import '../compiler/ast.ts' as {SyntaxKind}

let domains = ['node', 'puck']
let puckFile = RegExp('\\.puck$', 'i')
let puckModules = ['core', 'js', 'test']

export fn ImportVisitor(context, file: UnparsedFile) {
  let reportError = context.reportError.bind(context, file)
  let mut moduleScope

  fn importModule(mut i: ImportDirective, importedFile: UnparsedFile) {
    let contextFile = context.importFile(importedFile)
    let module: Module = contextFile.ast
    if not module then return context.defer(importedFile, || importModule(i, importedFile))

    i.file = contextFile
    i.module = module
    if i.specifier.kind == SyntaxKind.ObjectDestructure {
      i.specifier.members.forEach(|m| {
        if not (module.exports[m.property.name])
          then reportError(m, importedFile.fileName + ' has no export named ' + m.property.name)
      })
    }
    else if i.specifier.kind == SyntaxKind.AsteriskToken {
      i.specifier = {
        kind: SyntaxKind.ObjectDestructure
        members: module.exports.keys()
          .filter(|e| not moduleScope.getBinding(e))
          .map(|e| {
            let property = module.exports[e].identifier
            {
              kind: SyntaxKind.ObjectDestructureMember
              property
              local: property
            }
          })
      }
    }
  }

  Object.assign({}, visit.emptyVisitor, {

    visitModule: |self, m: Module| {
      moduleScope = m.scope
      m.expressions.forEach(|e| {
        if e.kind == SyntaxKind.ImportDirective {
          self.visitImportDirective(e)
        }
      })
    }

    visitImportDirective: |self, mut i: ImportDirective| {
      if i.domain.isNothing() {
        let importedFile = context.resolvePath(i.path, file)
        let path = importedFile.absolutePath
        let result: Result<{}, {}> = asResult(|| statSync(path))
        match result {
          Result::Ok(stat) => {
            if not stat.isFile()
              then reportError(i, 'Imported file $path is not a file')
          }
          Result::Err(error) => reportError(i, 'Imported file $path not found')
        }

        if puckFile.test(path) then importModule(i, importedFile)
      }
      else if i.domain.value[0] == 'puck' {
        if puckModules.indexOf(i.path) == -1
          then reportError(i, 'Invalid puck module ' + i.path)
        let importedFile = context.resolvePath(
          path.join(
            path.dirname(require.resolve('puck-lang/dist/bin/puck'))
            '../../lib/stdlib/' + i.path + '.puck'
          )
          file
        )
        importModule(i, importedFile)
      }
      else if domains.indexOf(i.domain.value[0]) == -1
        then reportError(i, 'Invalid import domain ' + i.domain.value[0])
    }
  })
}
