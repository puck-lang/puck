import 'puck:js' as {Object, undefined}
import '../ast/ast.puck' as {
  Block
  FunctionDeclaration
  Identifier
  ImplDeclaration
  Module
  ObjectDestructure
  TypeBound
  FunctionTypeBound
  NamedTypeBound
  TraitDeclaration
  TypeDeclaration
  TypeParameter
  VariableDeclaration

  ExportDirective
  ImportDirective

  AssignmentExpression
  BinaryExpression
  CallExpression
  ForExpression
  IfExpression
  LoopExpression
  UnaryExpression
  WhileExpression

  IndexAccess
  MemberAccess

  Token
  ReturnStatement

  ListLiteral
  BooleanLiteral
  NumberLiteral
  ObjectLiteral
  StringLiteral
}
import '../ast/visit.puck' as visit
import '../compiler/ast.ts' as {SyntaxKind}
import 'src/scope.puck' as {createScope}
import 'src/structure_visitor.puck' as {structureVisitor}
import 'src/types.puck' as {getType}

/// Visits and declares Traits, Types and toplevel function frames[1]
///
/// Traits and types are declared in this separate step so that we are sure
/// that complete type information is known during the checking step.
///
/// 1. function declaration withoud the body
export fn TypeVisitor(context, file) {
  let mut importDirective: ImportDirective
  let reportError = context.reportError.bind(context, file)

  Object.assign({}, visit.emptyVisitor, structureVisitor, {
    scope: createScope(context, file)
    reportError

    visitModule: |self, mut m: Module| {
      m.scope = self.scope
      let expressions = m.expressions.filter(|e|
        e.kind == SyntaxKind.ImportDirective or
        e.kind == SyntaxKind.TraitDeclaration or
        e.kind == SyntaxKind.TypeDeclaration or
        (e.kind == SyntaxKind.ExportDirective and (
          e.expression.kind == SyntaxKind.TraitDeclaration or
          e.expression.kind == SyntaxKind.TypeDeclaration
        ))
      )
      expressions.forEach(|mut e| {
        self.visitExpression(e)
        e.hoisted = true
      })
      expressions.forEach(self.visitExpression.bind(self))
    }
    visitObjectDestructure: |self, mut i: ObjectDestructure| {
      i.scope = self.scope
      i.members.forEach(|m| {
        if importDirective.module {
          let e = importDirective.module.exports[m.local.name]
          if e.expression.kind == SyntaxKind.TraitDeclaration {
            self.scope.defineType(e.expression)
            self.scope.define({name: m.local.name, mutable: false, token: m})
          }
          else if e.expression.kind == SyntaxKind.TypeDeclaration
            then self.scope.defineType(e.expression)
            else self.scope.define({name: m.local.name, mutable: false, token: m})
        }
        else self.scope.define({name: m.local.name, mutable: false, token: m})
      })
    }
    visitTraitDeclaration: |mut self, mut t: TraitDeclaration| {
      if not t.ty {
        t.ty = self.scope.defineType(t).ty
        t.binding = self.scope.define({
          name: t.name.name
          mutable: false
          token: t
          ty: t.ty
        })
      }
      else {
        self.scope = createScope(context, file, self.scope)
        t.scope = self.scope
        visit.walkTraitDeclaration(self, t)
        Object.assign(t.ty.functions, objectFromList(t.members.map(|m| [m.name.name, m.ty])))
        self.scope = self.scope.parent
      }
    }
    visitTypeDeclaration: |mut self, mut t: TypeDeclaration| {
      if not t.ty {
        t.ty = self.scope.defineType(t).ty
      }
      else {
        self.scope = createScope(context, file, self.scope)
        t.scope = self.scope
        visit.walkTypeDeclaration(self, t)

        t.ty.properties = objectFromList(t.properties.map(|p| [p.name.name, p.typeBound.ty]))

        self.scope = self.scope.parent
      }
    }
    visitTypeProperty: visit.walkingVisitor.visitTypeProperty

    visitExportDirective: |self, mut e: ExportDirective| {
      e.scope = self.scope
      visit.walkExportDirective(self, e)
    }
    visitImportDirective: |self, mut i: ImportDirective| {
      if i.hoisted then return undefined

      i.scope = self.scope
      if i.specifier.kind == SyntaxKind.Identifier
        then self.scope.define({name: i.specifier.name, mutable: false, token: i})
      else if i.specifier.kind == SyntaxKind.ObjectDestructure {
        importDirective = i
        visit.walkImportDirective(self, i)
      }
    }
  })
}
