import 'puck:js' as {Date, Error, asResult, undefined}
import 'node:path' as path
import 'node:vscode-languageserver' as {
  CompletionItemKind
  DiagnosticSeverity
}
import 'ast/ast.puck' as {Module, ExportedStatement, TraitDeclaration}
import 'ast/span.puck' as {Position, Span, ToSpan}
import 'pls/src/entities.puck' as {WorkspaceEdit, toUri, Diagnostic, CodeActionContext, Command}
import 'pls/src/imports.puck' as {bestImportPath, createImport}
import 'pls/completions.puck' as {CompletionVisitor}
import 'pls/definition.puck' as {Definition, DefinitionVisitor}
import 'pls/hover.puck' as {Hover, HoverVisitor}
import 'pls/position_visitor.puck' as {PositionVisitor}
import 'pls/signature.puck' as {SignatureHelp, SignatureVisitor}
import 'typeck/src/scope.puck' as {Scope}
import 'compiler.puck' as {CompilerContext}
import 'entities.puck' as {CompilationError, File}

export fn createServer(projectPath: String, sendDiagnostic: (String, Diagnostic) -> (), applyEdit: (WorkspaceEdit) -> ()) {
  let mut diagnostics: Map<String, Map<Num, CompilationError>> = Map.new()
  let mut globalExportMap: Map<String, Set<String>> = Map.new()
  let mut diagnosticId = 0

  fn reportError(file: File, token: ToSpan, error: CompilationError) {
    // print('reportError', (file.absolutePath, message))
    let id =
      match error {
        CompilationError::UndefinedVariable(name) => {
          diagnosticId += 1
        }
        CompilationError::TraitNotInScope => {
          diagnosticId += 1
        }
        _ => -1
      }
    if id >= 0 {
      diagnostics.entry(file.absolutePath).orInsertWith(|| Map.new())
        .set(id, error)
    }
    let span = token.span()
    sendDiagnostic(file.absolutePath, {
      severity: DiagnosticSeverity.Error
      range: {
        start: {line: span.start.line - 1, character: span.start.column - 1}
        end: {line: span.end.line - 1, character: span.end.column - 1}
      }
      code: id
      message: error.message()
      source: 'puck'
    })
  }

  fn onFileParsed(file: File) {
    let module: Module = file.ast.unwrap()
    module.exports.forEach(|(name, e)| {
      globalExportMap.entry(name).orInsertWith(|| Set.new())
        .add(file.absolutePath)
    })
  }

  let mut context = CompilerContext.new(projectPath, reportError, onFileParsed)

  fn normalizePath(filePath: String) {
    path->resolve(path->normalize(filePath)).transmute::<String>()
  }
  fn getAst(filePath: String) {
    context.files.get(normalizePath(filePath)).andThen(|f| f.ast)
  }

  let mut a = {}
  a->onClose = fn (filePath: String) {
    diagnostics.delete(filePath)
  }
  a->validateDocument = fn (filePath: String, contents: String, skipSyntaxErrors: Bool) {
    print('validateDocument')
    let startTime = Date->now().transmute::<Num>()

    let result = asResult(|| {
      let startTime = Date->now().transmute::<Num>()
      let mut file = File({
        isBin: false
        fileName: path->basename(filePath).transmute()
        absolutePath: path->resolve(path->normalize(filePath)).transmute()
        puck: contents
        js: None
        ast: None
        outFile: None
      })
      file = context.importFile(file, true, skipSyntaxErrors)

      let parseTime = Date->now().transmute::<Num>() - startTime
      print('Parse time $parseTime')

      if let Some(d) = diagnostics.get(filePath) {
        let mut dm = d
        dm.clear()
      }
      context.runTypeVisitor()
      let typeVisitorTime = Date->now().transmute::<Num>() - startTime - parseTime
      print('Type visitor time $typeVisitorTime')
      context.runImplVisitor()
      let implVisitorTime = Date->now().transmute::<Num>() - startTime - parseTime - typeVisitorTime
      print('Impl visitor time $implVisitorTime')
      context.runCheckerOnFile(file)
      let scopeVisitorTime = Date->now().transmute::<Num>() - startTime - parseTime - typeVisitorTime - implVisitorTime
      print('Scope visitor time $scopeVisitorTime')
    })
    let totalTime = Date->now().transmute::<Num>() - startTime
    print('Total time $totalTime')
    print('validateDocument completed')

    if let Result::Err(error) = result {
      if error.transmute::<String>() != 'Syntax Error' {
        print('Error:', error)
      }
    }
  }
  a->onCompletion = fn (filePath: String, position: Position) {
    print('onCompletion')
    let file = match context.files.get(normalizePath(filePath)) {
      Some(module) => module
      Option::None => return []
    }
    let module = match file.ast {
      Some(module) => module
      Option::None => return []
    }

    let mut visitor = CompletionVisitor.new(file, position)

    let result = asResult(|| {
      visitor.visitModule(module)
    })

    match result {
      Ok(completions) => {
        if let Some(completions) = visitor.completions {
          completions
        }
        else []
      }
      Err(error) => {
        print('completions Error:', (error, error->stack))
        []
      }
    }
  }
  a->onHover = fn (filePath: String, position: Position) -> Option<Hover> {
    print('onHover')
    let module = match getAst(filePath) {
      Some(module) => module
      Option::None => return None
    }

    let mut visitor = HoverVisitor.new(position)

    let result = asResult(|| {
      visitor.visitModule(module)
    })

    match result {
      Ok(_) => {
        print('onHover ok', visitor.hover)
        visitor.hover
      }
      Err(error) => {
        print('onHover Error:', (error, error->stack))
        None
      }
    }
  }
  a->onDefinition = fn (filePath: String, position: Position) -> List<Definition> {
    print('onDefinition')
    let file = match context.files.get(normalizePath(filePath)) {
      Some(module) => module
      Option::None => return []
    }
    let module = match file.ast {
      Some(module) => module
      Option::None => return []
    }

    let mut visitor = DefinitionVisitor.new(file, position)

    let result = asResult(|| {
      visitor.visitModule(module)
    })
    match result {
      Ok(_) => {
        print('onDefinition ok', visitor.definitions)
        visitor.definitions
      }
      Err(error) => {
        print('onDefinition Error:', (error, error->stack))
        []
      }
    }
  }
  a->onSignatureHelp = fn (filePath: String, position: Position) -> Option<SignatureHelp> {
    print('onSignatureHelp')
    let module = match getAst(filePath) {
      Some(module) => module
      Option::None => return None
    }

    let mut visitor = SignatureVisitor.new(position)

    let result = asResult(|| {
      visitor.visitModule(module)
    })
    match result {
      Ok(_) => {
        print('onSignature ok', visitor.signatureHelp)
        visitor.signatureHelp
      }
      Err(error) => {
        print('onDefinition Error:', (error, error->stack))
        None
      }
    }
  }
  a->onCodeAction = fn (filePath: String, span: Span, codeActionContext: CodeActionContext) -> List<Command> {
    let file = match context.files.get(normalizePath(filePath)) {
      Some(module) => module
      Option::None => return []
    }

    let result = asResult(|| {
    codeActionContext.diagnostics
      .iter()
      .filter(|d| d.code >= 0)
      .filterMap(|d| diagnostics.get(filePath).andThen(|errors| errors.get(d.code)))
      .flatMap(|error| {
        let list: List<Command> =
          match error {
            CompilationError::UndefinedVariable(name) => {
              globalExportMap.get(name).unwrapOrElse(|| Set.new())
                .iter()
                .map(|filePath| {
                  let path = bestImportPath(file, filePath)
                  let cmd = Command({
                    title: 'Import $name from $path'
                    command: 'puck.addImport'
                    arguments: [(name, file.absolutePath, filePath)]
                  })
                  cmd->['arguments'] = cmd->arguments
                  cmd
                })
                .collect()
            }
            CompilationError::TraitNotInScope {traitName, id} => {
              globalExportMap.get(traitName).unwrapOrElse(|| Set.new())
                .iter()
                .filterMap(|filePath| context.files.get(filePath))
                .filterMap(|file| file.ast)
                .filter(|module| {
                  module.exports.get(traitName)
                    .map(|e| match e.statement {
                      ExportedStatement::TraitDeclaration(t) => {
                        t.getType().providesType.unwrap().id.unwrap() == id
                      }
                      _ => false
                    })
                    .unwrapOr(false)
                })
                .map(|module| {
                  let path = bestImportPath(file, module.file.absolutePath)
                  let cmd = Command({
                    title: 'Import $traitName from $path'
                    command: 'puck.addImport'
                    arguments: [(traitName, file.absolutePath, module.file.absolutePath)]
                  })
                  cmd->['arguments'] = cmd->arguments
                  cmd
                })
                .collect()
            }
            _ => []
          }
        let iter: IntoIterator<Command> = list
        iter
      })
      .collect()
    })
    match result {
      Ok(a) => {
        print('onCodeAction ok', a)
        a
      }
      Err(error) => {
        print('onCodeAction Error:', (error, error->stack))
        []
      }
    }
  }
  a->onExecuteCommand = fn (command: Command) -> () {
    print('onExecuteCommand', command)
    if command.command == 'puck.reload' {
      context.files.clear()
      context.impls.clear()
      context.deferred.clear()
      globalExportMap.clear()
      diagnosticId = 0
    }
    else if command.command == 'puck.addImport' {
      let (name, importingPath, importedPath) = command->['arguments'].transmute()
      if let (Some(importingModule), Some(importedModule)) = (
        context.files.get(importingPath).andThen(|f| f.ast)
        context.files.get(importedPath).andThen(|f| f.ast)
      ) {
        let edit = createImport(name, importingModule, importedModule)
        print('edit', edit)
        let mut changes = ObjectMap.new()
        changes[toUri(importingPath)] = [edit]
        print('changes', changes)

        applyEdit({changes: [{
          textDocument: {uri: toUri(importingPath)}
          edits: [edit]
        }]})
      }
    }
  }
  a
}
