import 'puck:js' as {Array, asResult, console, process}
import 'node:child_process' as {execSync}
import 'node:fs' as fs
import 'node:path' as path
import 'ast/ast.puck' as {Token}
import 'compiler/ast.ts' as {SyntaxKind}

// Array.prototype.takeFrom = fn (self, predicate) {
//   let index = self.findIndex(predicate)
//   self.slice(index)
// }

export fn cmd(cmd) {
  let result = asResult(fn () {
    execSync(
      cmd
      {
        cwd: process.cwd()
        shell: '/bin/bash'
        env: {
          BASHOPTS: 'globstar:extglob'
          PATH: process.env.PATH
        }
      }
    )
  })

  if result.error {
    let stdout = result.error.stdout.toString()
    let stderr = result.error.stderr.toString()
    if stdout then console.log(stdout.trim())
    if stderr then console.error(stderr.trim())
    throw result.error
  }
  else if result.result.toString() then console.log(result.result.toString().trim())
}

/// Recursively and synchronously list all files in [directory]
export fn walkSync(directory, mut filelist = []) {
  let mut files = fs.readdirSync(directory)

  files.forEach(|fileName| {
    let file = path.join(directory, fileName)
    if fs.statSync(file).isDirectory()
      then walkSync(file, filelist)
      else filelist.push(file)
  })

  filelist
}

export fn flag(arguments, name, defaultValue) {
  let index = arguments.indexOf(name)
  if index >= 0 {
    let value = arguments[index + 1]
    arguments.splice(index, 2)
    value
  } else defaultValue
}

export fn isTypeScopeDeclaration(t: Token)
  then t.kind == SyntaxKind.TraitDeclaration
    or t.kind == SyntaxKind.TypeDeclaration
